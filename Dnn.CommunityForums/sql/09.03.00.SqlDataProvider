SET NOCOUNT ON 
GO


/* --------------------- */
/* issue 1517 - begin - add user mention notification option to user profile */

/* add UserMentionNotificationsEnabled to activeforums_UserProfiles */
IF NOT EXISTS(SELECT * FROM SYS.COLUMNS WHERE Name = N'UserMentionNotificationsEnabled' and Object_ID = Object_ID(N'{databaseOwner}[{objectQualifier}activeforums_UserProfiles]'))    
BEGIN
	ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserProfiles] ADD
	[UserMentionNotificationsEnabled] [bit] NOT NULL CONSTRAINT [DF_{objectQualifier}activeforums_UserProfiles_UserMentionNotificationsEnabled] DEFAULT(1)
END
GO

/* issue 1517 - end - add user mention notification option to user profile */

/* --------------------- */


/* issue 1518 - begin - add UserMentions table */

/* activeforums_UserMentions */
IF NOT EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_UserMentions]') AND type in (N'U'))
CREATE TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] (
    UserMentionId INT IDENTITY(1,1) NOT NULL, 
    PortalId INT NOT NULL,
    ModuleId INT NOT NULL,
    UserId INT NOT NULL,
    ContentId INT NOT NULL,
    DateMentioned DATETIME NOT NULL,
    CONSTRAINT PK_{objectQualifier}activeforums_UserMentions PRIMARY KEY CLUSTERED (UserMentionId ASC)
);

/*DF_activeforums_UserMentions_DateMentioned*/
IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}DF_{objectQualifier}activeforums_UserMentions_DateMentioned') AND type = 'D')
    ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] DROP CONSTRAINT [DF_{objectQualifier}activeforums_UserMentions_DateMentioned];
GO
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] ADD CONSTRAINT [DF_{objectQualifier}activeforums_UserMentions_DateMentioned] DEFAULT (GETUTCDATE()) FOR [DateMentioned];
GO

/* IX_activeforums_UserMentions_Unique1 */
IF EXISTS (SELECT * FROM sys.indexes WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_UserMentions') AND name = N'IX_{objectQualifier}activeforums_UserMentions_Unique1')
DROP INDEX [IX_{objectQualifier}activeforums_UserMentions_Unique1] ON {databaseOwner}{objectQualifier}activeforums_UserMentions 
GO
CREATE UNIQUE NONCLUSTERED INDEX IX_{objectQualifier}activeforums_UserMentions_Unique1
    ON {databaseOwner}[{objectQualifier}activeforums_UserMentions] 
    (
    PortalId ASC,
    UserId ASC,
    ContentId ASC
    ) 
GO

/* IX_activeforums_UserMentions_Unique2 */
IF EXISTS (SELECT * FROM sys.indexes WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_UserMentions') AND name = N'IX_{objectQualifier}activeforums_UserMentions_Unique2')
DROP INDEX [IX_{objectQualifier}activeforums_UserMentions_Unique2] ON {databaseOwner}{objectQualifier}activeforums_UserMentions 
GO
CREATE UNIQUE NONCLUSTERED INDEX IX_{objectQualifier}activeforums_UserMentions_Unique2
    ON {databaseOwner}[{objectQualifier}activeforums_UserMentions] 
    (
    ContentId ASC,
    PortalId ASC,
    UserId ASC
    ) 
GO

/* activeforums_UserMentions - cascade delete from activeforums_Content */
IF EXISTS (SELECT * FROM sys.foreign_keys WHERE object_id = OBJECT_ID(N'{databaseOwner}[FK_{objectQualifier}activeforums_UserMentions_Content]') AND parent_object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_UserMentions]'))
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] DROP CONSTRAINT 
[FK_{objectQualifier}activeforums_UserMentions_Content]
GO
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] ADD CONSTRAINT
	[FK_{objectQualifier}activeforums_UserMentions_Content] FOREIGN KEY (ContentId) 
	REFERENCES {databaseOwner}[{objectQualifier}activeforums_Content] (ContentId) 
	ON DELETE CASCADE 
GO

/* activeforums_UserMentions - cascade delete from activeforums_UserProfiles */
IF EXISTS (SELECT * FROM sys.foreign_keys WHERE object_id = OBJECT_ID(N'{databaseOwner}[FK_{objectQualifier}activeforums_UserMentions_UserProfiles]') AND parent_object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_UserMentions]'))
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] DROP CONSTRAINT 
[FK_{objectQualifier}activeforums_UserMentions_UserProfiles]
GO
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_UserMentions] ADD CONSTRAINT
	[FK_{objectQualifier}activeforums_UserMentions_UserProfiles] FOREIGN KEY (PortalId, UserId) 
	REFERENCES {databaseOwner}[{objectQualifier}activeforums_UserProfiles] (PortalId, UserId) 
	ON DELETE CASCADE 
GO

/* set EDITORTYPE setting to use editor with new mentions feature */
UPDATE {databaseOwner}[{objectQualifier}activeforums_Settings] SET SettingValue = '3' WHERE SettingName = 'EDITORTYPE' AND SettingValue = '3'
GO

/* issue 1518 - end - add UserMentions table */

/* --------------------- */

