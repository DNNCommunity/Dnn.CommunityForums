SET NOCOUNT ON 
GO
/* issue 1582 - begin - settings table overhaul */


/* drop old activeforums_Settings indexes and replace with new indexes */

IF EXISTS (SELECT * FROM sys.indexes WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_Settings') AND name = N'idx_{objectQualifier}activeforums_Settings_Opt1')
DROP INDEX [idx_{objectQualifier}activeforums_Settings_Opt1] ON {databaseOwner}{objectQualifier}activeforums_Settings 
GO
IF EXISTS (SELECT * FROM sys.indexes WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_Settings') AND name = N'idx_{objectQualifier}activeforums_Settings_Opt2')
DROP INDEX [idx_{objectQualifier}activeforums_Settings_Opt2] ON {databaseOwner}{objectQualifier}activeforums_Settings 
GO

IF (OBJECT_ID(N'{databaseOwner}[PK_{objectQualifier}activeforums_Settings]', 'PK') IS NOT NULL)
BEGIN
    ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_Settings] DROP CONSTRAINT [PK_{objectQualifier}activeforums_Settings]
END
GO

IF NOT EXISTS(SELECT * FROM SYS.COLUMNS WHERE Name = N'SettingsId' and Object_ID = Object_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings]'))    
BEGIN
	ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_Settings] 
		ADD SettingsId INT IDENTITY(1,1)
END
GO

ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_Settings] 
	ADD CONSTRAINT [PK_{objectQualifier}activeforums_Settings] PRIMARY KEY CLUSTERED ( [SettingsId] ASC  ) 
		WITH (PAD_INDEX  = OFF, STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS  = ON, ALLOW_PAGE_LOCKS  = ON) 

GO


IF EXISTS(SELECT * FROM sys.columns WHERE [name] = N'GroupKey' AND [object_id] = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings]'))
exec sp_rename '{databaseOwner}{objectQualifier}activeforums_Settings.GroupKey', 'SettingsKey', 'COLUMN'
GO


IF EXISTS (SELECT * FROM sys.indexes WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_Settings') AND name = N'IX_{objectQualifier}activeforums_Settings_Opt1')
DROP INDEX [IX_{objectQualifier}activeforums_Settings_Opt1]
    ON {databaseOwner}[{objectQualifier}activeforums_Settings];	
GO

CREATE UNIQUE NONCLUSTERED INDEX [IX_{objectQualifier}activeforums_Settings_Opt1]
    ON {databaseOwner}[{objectQualifier}activeforums_Settings]
	(	
	[ModuleId] ASC,
    [SettingsKey] ASC, 
	[SettingName] ASC
	)
    INCLUDE([SettingValue]);
GO

/* activeforums_Groups_Delete */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Groups_Delete]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Groups_Delete]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Groups_Delete]
@ModuleId int,
@ForumGroupId int
AS
DECLARE @GroupSettingsKey nvarchar(25)
SET @GroupSettingsKey = (SELECT GroupSettingsKey FROM {databaseOwner}{objectQualifier}activeforums_Groups WHERE ForumGroupId = @ForumGroupId AND ModuleId = @ModuleId)

/*DELETE REPLY ATTACHMENTS*/
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Attachments 
	WHERE AttachId IN (
		SELECT ca.AttachId FROM {databaseOwner}{objectQualifier}activeforums_Content_Attachments as ca 
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Content as c on c.ContentId = ca.ContentId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Replies as r on c.ContentId = r.ContentId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on ft.TopicId = r.TopicId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Groups as g on g.ForumGroupId = f.ForumGroupId
		WHERE g.ForumGroupId = @ForumGroupId)
/*DELETE TOPIC ATTACHMENTS*/
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Attachments 
	WHERE AttachId IN (
		SELECT ca.AttachId FROM {databaseOwner}{objectQualifier}activeforums_Content_Attachments as ca 
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Content as c on c.ContentId = ca.ContentId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Topics as t on c.ContentId = t.ContentId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on ft.TopicId = t.TopicId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Groups as g on g.ForumGroupId = f.ForumGroupId
		WHERE g.ForumGroupId = @ForumGroupId)

/*DELETE REPLY CONTENT */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Content WHERE ContentId IN (
	Select r.ContentId FROM {databaseOwner}{objectQualifier}activeforums_Replies as r 
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on ft.TopicId = r.TopicId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Groups as g on g.ForumGroupId = f.ForumGroupId
		WHERE g.ForumGroupId = @ForumGroupId)	

/*DELETE REPLIES */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Replies 
	WHERE TopicId IN (
		SELECT ft.TopicId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft 
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f ON f.ForumId = ft.ForumID
		WHERE f.ForumGroupId = @ForumGroupId)

/*DELETE TOPIC CONTENT */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Content WHERE ContentId IN (
	Select t.ContentId FROM {databaseOwner}{objectQualifier}activeforums_Topics as t
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on ft.TopicId = t.TopicId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Groups as g on g.ForumGroupId = f.ForumGroupId
		WHERE g.ForumGroupId = @ForumGroupId)	
/*DELETE TOPICS*/
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Topics WHERE TopicId IN (
		SELECT ft.TopicId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft INNER JOIN
		{databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId
		WHERE f.ForumGroupId = @ForumGroupId)

/*DELETE FORUM TRACKING*/
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Forums_Tracking 
	WHERE ForumId IN (
		SELECT ForumId FROM {databaseOwner}{objectQualifier}activeforums_Forums WHERE ForumGroupId = @ForumGroupId)
/*DELETE FORUM SETTINGS */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Settings
	WHERE ModuleId = @ModuleId AND SettingsKey IN (
		SELECT ForumSettingsKey FROM {databaseOwner}{objectQualifier}activeforums_Forums WHERE ForumGroupId = @ForumGroupId) 
/*DELETE FORUM SUBSCRIPTIONS */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Subscriptions
	WHERE ForumId IN (
		SELECT ForumId FROM {databaseOwner}{objectQualifier}activeforums_Forums WHERE ForumGroupId = @ForumGroupId) 
/*DELETE TOPICS CATEGORIES */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Topics_Categories
	WHERE TopicId IN (
		SELECT ft.TopicId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId WHERE f.ForumGroupId = @ForumGroupId) 
/*DELETE TOPICS TAGS */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Topics_Tags
	WHERE TopicId IN (
		SELECT ft.TopicId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId WHERE f.ForumGroupId = @ForumGroupId) 
/*DELETE TOPICS TRACKING */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Topics_Tracking
	WHERE TopicId IN (
		SELECT ft.TopicId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId WHERE f.ForumGroupId = @ForumGroupId)
/*DELETE TOPICS RATINGS */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Topics_Ratings
	WHERE TopicId IN (
		SELECT ft.TopicId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f on f.ForumId = ft.ForumId WHERE f.ForumGroupId = @ForumGroupId) 
/*DELETE FORUMTOPICS TABLE */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics 
	WHERE ForumId IN ( 
		SELECT f.ForumId FROM {databaseOwner}{objectQualifier}activeforums_Forums as f WHERE f.ForumGroupId = @ForumGroupId)
/*DELETE FORUMS */
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Forums WHERE ForumGroupId = @ForumGroupId


DELETE FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingsKey = @GroupSettingsKey AND ModuleId = @ModuleId

DELETE FROM {databaseOwner}{objectQualifier}activeforums_Groups WHERE ForumGroupId = @ForumGroupId AND ModuleId = @ModuleId
GO



/* activeforums_UI_ForumView */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_UI_ForumView]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_UI_ForumView]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_UI_ForumView]
@PortalId int,
@ModuleId int,
@UserId int,
@IsSuperUser bit = 0,
@ParentForumId int = -1,
@ForumIds nvarchar(2000) = ''
AS
IF @ParentForumId = -1
BEGIN
SELECT         G.ForumGroupId, G.ModuleId, G.GroupName, F.ForumId, F.ForumName, F.ForumDesc, F.Active AS ForumActive, F.Hidden AS ForumHidden, F.TotalTopics, 
					F.TotalReplies, F.ParentForumId, G.SortOrder AS GroupSort, 
					F.SortOrder AS ForumSort, G.Active AS GroupActive, G.Hidden AS GroupHidden,
					F.LastTopicId,
					F.LastReplyId,
					IsNull(c.Subject,'') as LastPostSubject,
					IsNull(c.AuthorName,'') as LastPostAuthorName,
					IsNull(c.AuthorId,-1) as LastPostAuthorId,
					IsNull(c.DateCreated,'') as LastPostDate,
					ISNULL(g.PrefixURL,'') as GroupPrefixURL,
					ISNULL(f.PrefixURL, '') as PrefixURL,
					LastRead = IsNull((SELECT LastAccessDate FROM {databaseOwner}{objectQualifier}activeforums_Forums_Tracking WHERE UserId = @UserId AND ForumId = F.ForumId),''),
					"AllowRSS" = (SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE ModuleId = F.ModuleId AND SettingsKey = F.ForumSettingsKey AND SettingName = 'ALLOWRSS'),
					P.*,
					ISNULL(t.URL,'') as TopicURL,
					COALESCE((SELECT  COUNT(*)
							  FROM         {databaseOwner}{objectQualifier}activeforums_Subscriptions
							  WHERE     (ForumId = F.ForumId) AND (TopicId = 0)) , 0) AS ForumSubscriberCount
	FROM         
		{databaseOwner}{objectQualifier}activeforums_Groups AS G INNER JOIN
		{databaseOwner}{objectQualifier}activeforums_Forums AS F ON G.ForumGroupId = F.ForumGroupId INNER JOIN
		{databaseOwner}{objectQualifier}activeforums_Permissions as p ON f.PermissionsId = p.PermissionsId INNER JOIN
		{databaseOwner}{objectQualifier}activeforums_Functions_Split(@ForumIds,';') as ft ON ft.id = f.ForumId LEFT OUTER JOIN
		{databaseOwner}{objectQualifier}activeforums_Topics as t ON t.TopicId = f.LastTopicId LEFT OUTER JOIN
		{databaseOwner}{objectQualifier}activeforums_Content as c ON c.ContentId = t.ContentId
	WHERE G.ModuleId = @ModuleId AND (G.Active = 1 AND F.Active = 1) AND (@ParentForumId = -1 OR (@ParentForumId > 0 AND F.ParentForumId = @ParentForumId))
	ORDER BY GroupSort, ForumSort
END
ELSE
BEGIN
SELECT         G.ForumGroupId, G.ModuleId, G.GroupName, F.ForumId, F.ForumName, F.ForumDesc, F.Active AS ForumActive, F.Hidden AS ForumHidden, F.TotalTopics, 
					F.TotalReplies, F.ParentForumId, G.SortOrder AS GroupSort, F.SortOrder AS ForumSort, G.Active AS GroupActive, G.Hidden AS GroupHidden,
					F.LastTopicId,
					F.LastReplyId,
					IsNull(c.Subject,'') as LastPostSubject,
					IsNull(c.AuthorName,'') as LastPostAuthorName,
					IsNull(c.AuthorId,-1) as LastPostAuthorId,
					IsNull(c.DateCreated,'') as LastPostDate,
					ISNULL(g.PrefixURL,'') as GroupPrefixURL,
					ISNULL(f.PrefixURL, '') as PrefixURL,
					ISNULL(t.URL, '') as TopicURL,
					COALESCE((SELECT  COUNT(*)
							  FROM         {databaseOwner}{objectQualifier}activeforums_Subscriptions
							  WHERE     (ForumId = F.ForumId) AND (TopicId = 0)) , 0) AS ForumSubscriberCount,
					LastRead = IsNull((SELECT LastAccessDate FROM {databaseOwner}{objectQualifier}activeforums_Forums_Tracking WHERE UserId = @UserId AND ForumId = F.ForumId),''),
					"AllowRSS" = (SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE ModuleId = F.ModuleId AND SettingsKey = F.ForumSettingsKey AND SettingName = 'ALLOWRSS'),
					P.*
	FROM         
		{databaseOwner}{objectQualifier}activeforums_Groups AS G INNER JOIN
		{databaseOwner}{objectQualifier}activeforums_Forums AS F ON G.ForumGroupId = F.ForumGroupId INNER JOIN
		{databaseOwner}{objectQualifier}activeforums_Permissions as p ON f.PermissionsId = p.PermissionsId LEFT OUTER JOIN
		{databaseOwner}{objectQualifier}activeforums_Topics as t ON t.TopicId = f.LastTopicId LEFT OUTER JOIN
		{databaseOwner}{objectQualifier}activeforums_Content as c ON c.ContentId = t.ContentId
					 
	WHERE G.ModuleId = @ModuleId AND (G.Active = 1 AND F.Active = 1) AND (@ParentForumId = -1 OR (@ParentForumId > 0 AND F.ParentForumId = @ParentForumId))
	ORDER BY GroupSort, ForumSort
END
If @UserId > 0 AND @ParentForumId =-1
	BEGIN
	SELECT TopicCount, ReplyCount, ViewCount, AnswerCount, RewardPoints, DateLastActivity, 
                      DateLastPost, SignatureDisabled, TrustLevel, AdminWatch, AttachDisabled, PrefDefaultSort, PrefDefaultShowReplies, 
                      PrefJumpLastPost, PrefTopicSubscribe, PrefSubscriptionType, PrefEmailFormat, PrefBlockAvatars, PrefBlockSignatures, PrefPageSize
                      
	FROM         {databaseOwner}{objectQualifier}activeforums_UserProfiles WHERE UserId = @UserId AND PortalId = @PortalId
	END


GO


/* activeforums_UI_TopicView -- remove LastPostId */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_UI_TopicView]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_UI_TopicView]
GO
CREATE PROCEDURE {databaseOwner}{objectQualifier}activeforums_UI_TopicView
@PortalId int,
@ModuleId int,
@ForumId int,
@TopicId int,
@UserId int,
@RowIndex int, 
@MaxRows int,
@IsSuperUser bit = 0,
@Sort varchar(10) = 'ASC'
AS
--Forum/Group/Topic Info
DECLARE @LastPostId int
DECLARE @ReplyCount int
SET @ReplyCount = (Select Count(ReplyId) from {databaseOwner}{objectQualifier}activeforums_Replies WHERE TopicId = @TopicId AND IsDeleted = 0 AND IsApproved = 1)
DECLARE @Tags nvarchar(1000)
SET @Tags= RTRIM(IsNull({databaseOwner}{objectQualifier}activeforums_Topics_GetTags(@TopicId),''))
BEGIN
SELECT     
	v.ForumGroupId, 
	v.ModuleId, 
	v.GroupName, 
	v.GroupActive, 
	v.GroupHidden, 
	v.ForumId, 
	v.ParentForumId, 
	v.ForumName, 
	v.ForumDesc, 
	v.ForumActive, 
	v.ForumHidden, 
	v.TotalTopics, 
	ISNULL(v.TotalReplies, 0) AS TotalReplies,
	v.GroupSettingsKey,
	v.ForumSettingsKey,
	TopicTemplateId = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICTEMPLATEID' and SettingsKey = v.ForumSettingsKey),0),
	
						  IsNull((SELECT     SettingValue
							FROM          {databaseOwner}{objectQualifier}activeforums_Settings AS {objectQualifier}activeforums_Settings_1
							WHERE      (SettingName = 'ALLOWRSS') AND (SettingsKey = v.ForumSettingsKey)),0) AS AllowRSS,
						  IsNull((SELECT     SettingValue
							FROM          {databaseOwner}{objectQualifier}activeforums_Settings AS {objectQualifier}activeforums_Settings_3
							WHERE      (SettingName = 'ALLOWHTML') AND (SettingsKey = v.ForumSettingsKey)),0) AS AllowHTML,
						  IsNull((SELECT     SettingValue
							FROM          {databaseOwner}{objectQualifier}activeforums_Settings AS activeforums_Settings_3
							WHERE      (SettingName = 'ALLOWLIKES') AND (SettingsKey = v.ForumSettingsKey)),0) AS AllowLikes,
						  IsNull((SELECT     SettingValue
							FROM          {databaseOwner}{objectQualifier}activeforums_Settings AS {objectQualifier}activeforums_Settings_2
							WHERE      (SettingName = 'ALLOWSCRIPT') AND (SettingsKey = v.ForumSettingsKey)),0) AS AllowScript,
							IsNull((SELECT     SettingValue
							FROM          {databaseOwner}{objectQualifier}activeforums_Settings
							WHERE      (SettingName = 'ALLOWTAGS') AND (SettingsKey = v.ForumSettingsKey)),0) AS AllowTags,
							 FT.TopicId,
						  (SELECT     ISNULL(AVG(Rating), 0) AS Expr1
							FROM          {databaseOwner}{objectQualifier}activeforums_Topics_Ratings
							WHERE      (TopicId = @TopicId)) AS TopicRating,
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.DateCreated,'') ELSE IsNull(R.DateCreated,'') END AS LastPostDate, 
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.AuthorId,'') ELSE IsNull(R.AuthorId,'') END AS LastPostAuthorId, 
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.AuthorName,'') ELSE IsNull(R.AuthorName,'') END AS LastPostAuthorName,
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.UserName,'') ELSE IsNull(R.Username,'') END AS LastPostUserName,
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.FirstName,'') ELSE IsNull(R.FirstName,'') END AS LastPostFirstName, 
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.LastName,'') ELSE IsNull(R.LastName,'') END AS LastPostLastName, 
						CASE WHEN FT.LastReplyId is NULL THEN IsNull(T.DisplayName,'') ELSE IsNull(R.DisplayName,'') END AS LastPostDisplayName, 
                        T.Subject, T.Summary, T.Body, T.AuthorId, T.AuthorName, T.Username, T.FirstName, T.LastName, 
					  T.DisplayName, T.DateCreated, T.DateUpdated, T.ViewCount, @ReplyCount as ReplyCount, T.IsPinned, T.IsLocked, T.StatusId, T.TopicIcon, T.TopicType, @Tags as Tags,ISNULL(t.TopicData,'') as TopicData,
					  {databaseOwner}{objectQualifier}activeforums_Poll.PollID,
					aft.NextTopic, 
					aft.PrevTopic,
					t.URL,
					T.AuthorName as TopicAuthor,
                     aft.IsAnnounce, aft.AnnounceStart, aft.AnnounceEnd, aft.IsApproved, aft.IsDeleted, aft.IsRejected, aft.IsArchived, aft.IsLocked, aft.IsPinned, aft.TopicIcon, aft.TopicType, aft.Priority,
                    aft.ContentId, C.IPAddress,
					COALESCE((SELECT COUNT(*)
							  FROM         {databaseOwner}{objectQualifier}activeforums_Subscriptions
							  WHERE     (ForumId = @ForumId) AND (TopicId = @TopicId)), 0) AS TopicSubscriberCount,
					COALESCE((SELECT COUNT(*)
							  FROM         {databaseOwner}{objectQualifier}activeforums_Subscriptions
							  WHERE     (ForumId = @ForumId) AND (TopicId = 0)), 0) AS ForumSubscriberCount
FROM
	{databaseOwner}{objectQualifier}activeforums_Topics aft INNER JOIN          
	{databaseOwner}{objectQualifier}activeforums_ForumTopics AS FT ON aft.TopicId = FT.TopicId INNER JOIN
					  {databaseOwner}{objectQualifier}vw_activeforums_GroupForum AS v ON FT.ForumId = v.ForumId INNER JOIN
					  {databaseOwner}{objectQualifier}vw_activeforums_ForumTopics AS T ON FT.TopicId = T.TopicId LEFT OUTER JOIN
					  {databaseOwner}{objectQualifier}vw_activeforums_ForumReplies AS R ON FT.LastReplyId = R.ReplyId AND FT.LastReplyId IS NOT NULL LEFT OUTER JOIN
					  {databaseOwner}{objectQualifier}activeforums_Poll ON T.TopicId = {databaseOwner}{objectQualifier}activeforums_Poll.TopicId
                      LEFT OUTER JOIN {databaseOwner}{objectQualifier}activeforums_Content AS C ON C.ContentId = aft.ContentId
WHERE     (v.ForumActive = 1) AND (v.ModuleId = @ModuleId) AND (v.ForumId = @ForumId) AND (FT.TopicId = @TopicId)
END
--Forum Security
BEGIN
	Select p.* from {databaseOwner}{objectQualifier}activeforums_Permissions as p INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as f ON f.PermissionsId = p.PermissionsId WHERE f.ForumId = @ForumId
	
END
--Get Topic and Replies
	SELECT	ForumId, TopicId, ReplyId, [Subject], Summary, AuthorId, StatusId, AuthorName, UserName, FirstName, LastName, 
			DisplayName, DateCreated, DateUpdated, Body, TopicCount, ReplyCount, ViewCount, AnswerCount,
			RewardPoints, UserDateCreated, DateLastActivity, UserCaption, [Signature], SignatureDisabled,
			UserPostCount, UserTotalPoints,IPAddress,AvatarDisabled,MemberSince,
			ContentId,IsUserOnline,ReplyToId,	UserRoles = {databaseOwner}{objectQualifier}activeforums_UserProfiles_GetUserRoles(AuthorId, @PortalID, GETUTCDATE(),0),IsApproved,
			@Tags as Tags
			
	FROM
(
			SELECT	T.ForumId, T.TopicId, T.ReplyId, T.Subject, T.Summary, T.AuthorId, T.StatusId, IsNull(T.AuthorName,'anon') as AuthorName, IsNull(T.Username,IsNull(T.AuthorName,'anon')) as Username,
			IsNull(T.FirstName,'') as FirstName, IsNull(T.LastName,'') as LastName,
            IsNull(T.DisplayName,T.AuthorName) as DisplayName,
			T.DateCreated, T.DateUpdated, C.Body, IsNull(P.TopicCount,0) as TopicCount, IsNull(P.ReplyCount,0) as ReplyCount,
			IsNull(P.ViewCount,0) as ViewCount, IsNull(P.AnswerCount,0) as AnswerCount, IsNull(P.RewardPoints,0) as RewardPoints,
			IsNull(P.DateCreated,'') AS UserDateCreated, IsNull(P.DateLastActivity,'') as DateLastActivity, 
			IsNull(P.UserCaption,'') as UserCaption, IsNull(P.Signature,'') as [Signature], IsNull(P.SignatureDisabled,0) as SignatureDisabled, 
			UserPostCount = (IsNull(P.TopicCount,0) + IsNull(P.ReplyCount,0)), 
			UserTotalPoints = (IsNull(P.TopicCount,0) + IsNull(P.ReplyCount,0) + IsNull(P.AnswerCount,0) + IsNull(P.RewardPoints,0)),
			C.IPAddress, IsNull(P.AvatarDisabled,0) as AvatarDisabled,
			IsNull(P.DateCreated,'') as MemberSince,
			C.ContentId, IsUserOnline = (CASE WHEN DATEDIFF(mi,p.DateLastActivity,GETUTCDATE()) <=1 THEN 1 ELSE 0 END),T.ReplyToId,
			1 AS IsApproved /* only approved content is returned from vw_activeforums_TopicView */,
			ROW_NUMBER() OVER (Order By 
								CASE
									WHEN @Sort = 'DESC' THEN T.DateCreated END DESC,
								CASE 
									WHEN @Sort = 'ASC' THEN T.DateCreated END ASC
								) as RowRank
			FROM	{databaseOwner}{objectQualifier}vw_activeforums_TopicView AS T INNER JOIN
					{databaseOwner}{objectQualifier}activeforums_Content AS C ON T.ContentId = C.ContentId LEFT OUTER JOIN
					{databaseOwner}{objectQualifier}activeforums_UserProfiles AS P ON C.AuthorId = P.UserId AND P.PortalId = @PortalId
			WHERE     (T.TopicId = @TopicId)
) 
		AS TopicWithRowNumbers
		WHERE RowRank > @RowIndex AND RowRank <= (@RowIndex + @MaxRows)

--Get Attachments
SELECT     A.AttachId, A.ContentId, A.UserID, A.[FileName], A.ContentType, A.FileSize, A.FileID
FROM        {databaseOwner}{objectQualifier}activeforums_Attachments AS A inner join
			{databaseOwner}{objectQualifier}vw_activeforums_TopicView AS T ON A.ContentId = T.ContentId
WHERE     (T.TopicId = @TopicId AND (A.AllowDownload = 1 OR A.AllowDownload IS NULL))

--Update View Count
UPDATE {databaseOwner}{objectQualifier}activeforums_Topics SET ViewCount = (ViewCount+1) WHERE TopicId = @TopicId
If @UserId > 0
BEGIN
SELECT @LastPostId = IsNull(LastReplyId,0) FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics WHERE ForumId = @ForumId AND TopicId = @TopicId
exec {databaseOwner}{objectQualifier}activeforums_Forums_Tracking_UpdateUser @ModuleId, @UserId, @ForumId	
SET @LastPostId = IsNull(@LastPostId,0)
exec {databaseOwner}{objectQualifier}activeforums_Topics_Tracking_UpdateUser @ForumId, @TopicId, @LastPostId, @UserId
exec {databaseOwner}{objectQualifier}activeforums_UserProfiles_UpdateActivity @PortalId, @UserId
END

GO


/*activeforums_UI_TopicsView */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[activeforums_UI_TopicsView]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[activeforums_UI_TopicsView]
GO
CREATE PROCEDURE [dbo].[activeforums_UI_TopicsView]
@PortalId int,
@ModuleId int,
@ForumId int,
@UserId int,
@RowIndex int = 0, 
@MaxRows int = 20,
@IsSuperUser bit = 0,
@SortColumn nvarchar(25) = 'ReplyCreated'
AS
--Forum/Group Info
DECLARE @PrefixURL nvarchar(255)
DECLARE @GroupPrefix nvarchar(255)
SET @GroupPrefix = (SELECT g.PrefixURL from [dbo].activeforums_Groups as g INNER JOIN [dbo].activeforums_Forums as f ON f.ForumGroupId = g.ForumGroupId WHERE f.ForumId=@ForumId)
SET @PrefixURL = (SELECT PRefixURL from [dbo].activeforums_Forums WHERE ForumId = @ForumId)
IF @PrefixURL <> '' OR @PrefixURL IS NOT NULL
	SET @PrefixURL = '/' + @PrefixURL + '/'
IF @GroupPrefix <> '' OR @GroupPrefix IS NOT NULL
	SET @PrefixURL = '/' + @GroupPrefix + @PrefixURL 
BEGIN
	SELECT v.ForumName, v.GroupName, v.ForumGroupId, v.ForumDesc,
		TopicsTemplateId = IsNull((SELECT SettingValue FROM [dbo].activeforums_Settings WHERE ModuleId = @ModuleId AND SettingName = 'TOPICSTEMPLATEID' and SettingsKey = v.ForumSettingsKey),0),
		AllowRSS = IsNull((SELECT SettingValue FROM [dbo].activeforums_Settings WHERE ModuleId = @ModuleId AND SettingName = 'ALLOWRSS' and SettingsKey = v.ForumSettingsKey),0),
		TopicRowCount = IsNull((SELECT Count(t.TopicId) FROM [dbo].activeforums_Topics as t inner join [dbo].activeforums_ForumTopics as ft on t.topicid = ft.topicid WHERE ft.ForumId = @ForumId AND t.IsApproved = 1 and t.IsDeleted = 0),0),
		IsSubscribedForum = IsNull((SELECT ID FROM [dbo].activeforums_Subscriptions WHERE ModuleId = @ModuleId AND ForumId = @ForumId AND TopicId = 0 AND UserId = @UserId),0),
					COALESCE((SELECT COUNT(*)
							  FROM         [dbo].activeforums_Subscriptions
							  WHERE    (ModuleId = @ModuleId) AND (ForumId = @ForumId) AND (TopicId = 0)), 0) AS ForumSubscriberCount
		
		 FROM [dbo].vw_activeforums_GroupForum as v WHERE v.ForumActive = 1 AND v.ModuleId = @ModuleId AND v.ForumId = @ForumId
END
--Forum Security
BEGIN
	Select p.* from [dbo].activeforums_Permissions as p INNER JOIN [dbo].activeforums_Forums as f ON f.PermissionsId = p.PermissionsId WHERE f.ModuleId = @ModuleId AND f.ForumId = @ForumId
	
END
--Get Sub Forums
 exec [dbo].activeforums_UI_ForumView @PortalId, @ModuleId,@UserId,@IsSuperUser, @ForumId

--Get Topics

SELECT 
	ForumId,
	LastReplyId,
	TopicId,
    ContentId,
    TopicContentId,
	ViewCount,
	ReplyCount,
	IsLocked,
	IsPinned,
    IsApproved,
    IsDeleted,
    IsRejected,
    IsArchived,
	TopicIcon,
	StatusId,
	IsAnnounce,
	AnnounceStart,
	AnnounceEnd,
	TopicType,
    Priority,
	[Subject],
	Summary,
	AuthorId,
	AuthorName,
	Body,
	DateCreated,
	AuthorUserName,
	AuthorFirstName,
	AuthorLastName,
	AuthorDisplayName,
	LastReplyContentId,
	LastReplySubject,
	LastReplySummary,
	LastReplyAuthorId,
	LastReplyAuthorName,
	LastReplyUserName,
	LastReplyFirstName,
	LastReplyLastName,
	LastReplyDisplayName,
	LastReplyDate,
	TopicRating,
	UserLastReplyRead,
	UserLastTopicRead,
	TopicURL,
	TopicData,
	FullURL,
	TopicSubscriberCount,
	ForumSubscriberCount
	FROM (

SELECT 
	f.ForumId,
	IsNull(f.LastReplyId,0) as LastReplyId,
	t.TopicId,
    CASE WHEN rc.ContentId IS NULL THEN c.ContentId ELSE rc.ContentId END as ContentId,
    t.ContentId as TopicContentId,
	t.ViewCount,
	t.ReplyCount,
	t.IsLocked,
	t.IsPinned,
    t.IsApproved,
    t.IsDeleted,
    t.IsRejected,
    t.IsArchived,
	IsNull(t.TopicIcon,'') as TopicIcon,
	t.StatusId,
	t.IsAnnounce,
	t.AnnounceStart,
	t.AnnounceEnd,
	t.TopicType, 
    t.Priority,
	c.Subject,
	IsNull(c.Summary,'') as Summary,
	IsNull(c.AuthorId,-1) as AuthorId,
	IsNull(c.AuthorName,'') as AuthorName,
	c.Body,
	c.DateCreated,
	IsNull(u.Username,'') as AuthorUserName,
	IsNull(u.FirstName,'') as AuthorFirstName,
	IsNull(u.LastName,'') as AuthorLastName,
	IsNull(u.DisplayName,'') as AuthorDisplayName,
	CASE WHEN rc.ContentId IS NULL THEN c.ContentId ELSE rc.ContentId END as LastReplyContentId,
	CASE WHEN rc.Subject IS NULL THEN c.Subject ELSE rc.Subject END as LastReplySubject,
	CASE WHEN rc.Summary IS NULL THEN IsNull(c.Summary,'') ELSE rc.Summary END as LastReplySummary,
	CASE WHEN rc.AuthorId IS NULL THEN c.AuthorId ELSE rc.AuthorId END as LastReplyAuthorId,
	CASE WHEN rc.AuthorName IS NULL THEN IsNull(c.AuthorName,'') ELSE rc.AuthorName END  as LastReplyAuthorName,
	CASE WHEN ru.Username IS NULL THEN IsNull(u.UserName,'') ELSE ru.UserName END as LastReplyUserName,
	CASE WHEN ru.FirstName IS NULL THEN IsNULL(u.FirstName,'') ELSE ru.FirstName END as LastReplyFirstName,
	CASE WHEN ru.LastName IS NULL THEN IsNull(u.LastName,'') ELSE ru.LastName END as LastReplyLastName,
	CASE WHEN ru.DisplayName IS NULL THEN IsNull(IsNull(u.DisplayName,rc.AuthorName),'') ELSE ru.DisplayName END as LastReplyDisplayName,
	CASE WHEN rc.DateCreated IS NULL THEN c.DateCreated ELSE rc.DateCreated END  as LastReplyDate,
	CASE WHEN FT.MaxReplyRead > TT.LastReplyId OR TT.LastReplyID IS NULL THEN ISNULL(FT.MaxReplyRead,0) ELSE TT.LastReplyId END AS UserLastReplyRead, 
	CASE WHEN FT.MaxTopicRead > TT.TopicId OR TT.TopicId IS NULL THEN ISNULL(FT.MaxTopicRead,0) ELSE TT.TopicId END AS UserLastTopicRead,
	t.URL as TopicURL,
	IsNull(t.TopicData,'') as TopicData,
	CASE WHEN ISNULL(t.URL,'') <> '' THEN @PrefixURL + t.URL ELSE '' END as FullURL,
	(SELECT     ISNULL(AVG(Rating), 0) AS Expr1
                            FROM	[dbo].activeforums_Topics_Ratings
                            WHERE      (TopicId = T.TopicId)) AS TopicRating, 
						ROW_NUMBER() OVER (ORDER BY T.IsPinned DESC, T.Priority DESC,
								CASE
									WHEN @SortColumn = 'ReplyCreated' THEN 
										CASE WHEN rc.DateCreated IS NULL THEN c.DateCreated ELSE rc.DateCreated END
									WHEN @SortColumn = 'TopicCreated' THEN
										c.DateCreated
								END DESC) as RowRank,
					COALESCE((SELECT COUNT(*)
							  FROM         [dbo].activeforums_Subscriptions
							  WHERE     (ModuleId = @ModuleId) AND (ForumId = @ForumId) AND (TopicId = T.TopicId)), 0) AS TopicSubscriberCount,
					COALESCE((SELECT COUNT(*)
							  FROM         [dbo].activeforums_Subscriptions
							  WHERE     (ModuleId = @ModuleId) AND (ForumId = @ForumId) AND (TopicId = 0)), 0) AS ForumSubscriberCount
				
		FROM	[dbo].activeforums_ForumTopics AS f INNER JOIN
				[dbo].activeforums_Topics as t on f.TopicId = t.TopicId INNER JOIN
				[dbo].activeforums_Content as c on t.ContentId = c.ContentId LEFT OUTER JOIN
				[dbo].Users as u on c.AuthorId = u.UserId LEFT OUTER JOIN
				[dbo].activeforums_Replies as r on f.LastReplyId = r.ReplyId LEFT OUTER JOIN
				[dbo].activeforums_Content as rc on r.ContentId = rc.ContentId LEFT OUTER JOIN
				[dbo].Users as ru on rc.AuthorId = ru.UserId LEFT OUTER JOIN				
                [dbo].activeforums_Topics_Tracking AS TT ON T.TopicId = TT.TopicId AND TT.UserId = @UserId LEFT OUTER JOIN
				[dbo].activeforums_Forums_Tracking as FT ON f.ForumId = FT.ForumId AND FT.UserId = @UserId
	
		WHERE     (f.ForumId = @ForumId AND t.IsApproved = 1 AND t.IsDeleted = 0)	
		) AS TopicsWithRowNumbers
	WHERE RowRank > @RowIndex AND RowRank <= (@RowIndex + @MaxRows)
	IF @RowIndex = 0
	BEGIN
		SELECT 
        f.ForumId,
	    IsNull(f.LastReplyId,0) as LastReplyId,
	    t.TopicId,
	    CASE WHEN rc.ContentId IS NULL THEN c.ContentId ELSE rc.ContentId END as ContentId,
        t.ContentId AS TopicContentId,
	    t.ViewCount,
	    t.ReplyCount,
	    t.IsLocked,
	    t.IsPinned,
        t.IsApproved,
        t.IsDeleted,
        t.IsRejected,
        t.IsArchived,
	    IsNull(t.TopicIcon,'') as TopicIcon,
	    t.StatusId,
	    t.IsAnnounce,
	    t.AnnounceStart,
	    t.AnnounceEnd,
	    t.TopicType,
        t.Priority,
	    c.Subject,
	    IsNull(c.Summary,'') as Summary,
	    IsNull(c.AuthorId,-1) as AuthorId,
	    IsNull(c.AuthorName,'') as AuthorName,
	    c.Body,
	    c.DateCreated,
	    IsNull(u.Username,'') as AuthorUserName,
	    IsNull(u.FirstName,'') as AuthorFirstName,
	    IsNull(u.LastName,'') as AuthorLastName,
	    IsNull(u.DisplayName,'') as AuthorDisplayName,
	    CASE WHEN rc.ContentId IS NULL THEN c.ContentId ELSE rc.ContentId END as LastReplyContentId,
	    CASE WHEN rc.Subject IS NULL THEN c.Subject ELSE rc.Subject END as LastReplySubject,
	    CASE WHEN rc.Summary IS NULL THEN IsNull(c.Summary,'') ELSE rc.Summary END as LastReplySummary,
	    CASE WHEN rc.AuthorId IS NULL THEN c.AuthorId ELSE rc.AuthorId END as LastReplyAuthorId,
	    CASE WHEN rc.AuthorName IS NULL THEN IsNull(c.AuthorName,'') ELSE rc.AuthorName END  as LastReplyAuthorName,
	    CASE WHEN ru.Username IS NULL THEN IsNull(u.UserName,'') ELSE ru.UserName END as LastReplyUserName,
	    CASE WHEN ru.FirstName IS NULL THEN IsNULL(u.FirstName,'') ELSE ru.FirstName END as LastReplyFirstName,
	    CASE WHEN ru.LastName IS NULL THEN IsNull(u.LastName,'') ELSE ru.LastName END as LastReplyLastName,
	    CASE WHEN ru.DisplayName IS NULL THEN IsNull(IsNull(u.DisplayName,rc.AuthorName),'') ELSE ru.DisplayName END as LastReplyDisplayName,
	    CASE WHEN rc.DateCreated IS NULL THEN c.DateCreated ELSE rc.DateCreated END  as LastReplyDate,
	    CASE WHEN FT.MaxReplyRead > TT.LastReplyId OR TT.LastReplyID IS NULL THEN ISNULL(FT.MaxReplyRead,0) ELSE TT.LastReplyId END AS UserLastReplyRead, 
	    CASE WHEN FT.MaxTopicRead > TT.TopicId OR TT.TopicId IS NULL THEN ISNULL(FT.MaxTopicRead,0) ELSE TT.TopicId END AS UserLastTopicRead,
	    t.URL as TopicURL,
	    IsNull(t.TopicData,'') as TopicData,
	    CASE WHEN ISNULL(t.URL,'') <> '' THEN @PrefixURL + t.URL ELSE '' END as FullURL,
	    (SELECT     ISNULL(AVG(Rating), 0) AS Expr1
                                FROM	[dbo].activeforums_Topics_Ratings
                                WHERE      (TopicId = T.TopicId)) AS TopicRating, 
						    ROW_NUMBER() OVER (ORDER BY T.IsPinned DESC,
								    CASE
									    WHEN rc.DateCreated IS NULL THEN c.DateCreated ELSE rc.DateCreated END DESC
											    ) as RowRank,
					    COALESCE((SELECT COUNT(*)
							      FROM         [dbo].activeforums_Subscriptions
							      WHERE     (ModuleId = @ModuleId) AND (ForumId = @ForumId) AND (TopicId = T.TopicId)), 0) AS TopicSubscriberCount,
					    COALESCE((SELECT COUNT(*)
							      FROM         [dbo].activeforums_Subscriptions
							      WHERE     (ModuleId = @ModuleId) AND (ForumId = @ForumId) AND (TopicId = 0)), 0) AS ForumSubscriberCount
				
		    FROM	[dbo].activeforums_ForumTopics AS f INNER JOIN
				    [dbo].activeforums_Topics as t on f.TopicId = t.TopicId INNER JOIN
				    [dbo].activeforums_Content as c on t.ContentId = c.ContentId LEFT OUTER JOIN
				    [dbo].Users as u on c.AuthorId = u.UserId LEFT OUTER JOIN
				    [dbo].activeforums_Replies as r on f.LastReplyId = r.ReplyId LEFT OUTER JOIN
				    [dbo].activeforums_Content as rc on r.ContentId = rc.ContentId LEFT OUTER JOIN
				    [dbo].Users as ru on rc.AuthorId = ru.UserId LEFT OUTER JOIN				
                    [dbo].activeforums_Topics_Tracking AS TT ON T.TopicId = TT.TopicId AND TT.UserId = @UserId LEFT OUTER JOIN
				    [dbo].activeforums_Forums_Tracking as FT ON f.ForumId = FT.ForumId AND FT.UserId = @UserId
	
		    WHERE     (f.ForumId = @ForumId AND t.IsApproved = 1 AND t.IsDeleted = 0 AND T.IsAnnounce = 1 AND T.AnnounceStart <= GETUTCDATE() AND T.AnnounceEnd >= GETUTCDATE())
		    ORDER BY T.IsPinned DESC, c.DateCreated DESC, rc.DateCreated DESC
	END
BEGIN
If @UserId > 0
	BEGIN
	exec [dbo].activeforums_Forums_Tracking_UpdateUser @ModuleId, @UserId, @ForumId
	exec [dbo].activeforums_UserProfiles_UpdateActivity @PortalId, @UserId
	END
END

GO

IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Subscriptions_Subscribers]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Subscriptions_Subscribers]
GO

CREATE PROCEDURE  {databaseOwner}[{objectQualifier}activeforums_Subscriptions_Subscribers](@PortalId int, @ForumId int, @TopicId int, @SubType int)
AS

DECLARE @CanSubscribe nvarchar(256)
SET @CanSubscribe = (
					SELECT p.CanSubscribe
					FROM {databaseOwner}{objectQualifier}activeforums_Forums as f 
					INNER JOIN {databaseOwner}{objectQualifier}activeforums_Permissions as p 
					ON p.PermissionsId = f.PermissionsId
					WHERE f.ForumId = @ForumId
					)
					
DECLARE @subs TABLE (userid int, email nvarchar(255), topicsubscriber bit)

/* get topic subscribers who are not superusers so need to check against roles allowed to be subscribers */
INSERT INTO @subs 
	(userid, email, topicsubscriber)
	(SELECT s.UserId, u.Email, 1 AS topicsubscriber  
		FROM {databaseOwner}{objectQualifier}activeforums_Subscriptions AS s 		
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums AS f 
		ON f.ForumId = s.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}Users AS u 
		ON  u.UserID = s.UserId 
		AND u.IsSuperUser = 0 
		AND u.IsDeleted = 0
		INNER JOIN {databaseOwner}{objectQualifier}UserPortals AS up 
		ON  up.UserId = u.UserID
		AND up.PortalId = @PortalId
		AND up.IsDeleted = 0
		AND up.Authorised = 1
		INNER JOIN {databaseOwner}{objectQualifier}UserRoles AS ur 
		ON ur.UserID = u.UserID 
		AND (
					(ur.EffectiveDate IS NULL AND ur.ExpiryDate >= GETDATE()) /* DNN platform user roles still use native GETDATE() not GETUTCDATE() */
				OR (ur.EffectiveDate IS NULL AND ur.ExpiryDate IS NULL)
				OR (ur.EffectiveDate <= GETDATE() AND ur.ExpiryDate IS NULL)
				OR (ur.EffectiveDate <= GETDATE() AND ur.ExpiryDate >= GETDATE())
			)
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Functions_Split(@CanSubscribe,';') AS r 
		ON r.ID = ur.RoleId
		WHERE 	(s.Mode = @SubType)
		AND 	(s.TopicId = @TopicId)
		AND (NOT EXISTS (SELECT * FROM @subs WHERE userid = s.UserId))
	)

/* get forum subscribers who are not superusers so need to check against roles allowed to be subscribers */
INSERT INTO @subs 
	(userid, email, topicsubscriber)
	(SELECT s.UserId, u.Email, 0 AS topicsubscriber  
		FROM {databaseOwner}{objectQualifier}activeforums_Subscriptions AS s 		
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums AS f 
		ON f.ForumId = s.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}Users AS u 
		ON  u.UserID = s.UserId 
		AND u.IsSuperUser = 0
		AND u.IsDeleted = 0
		INNER JOIN {databaseOwner}{objectQualifier}UserPortals AS up 
		ON  up.UserId = u.UserID
		AND up.PortalId = @PortalId
		AND up.IsDeleted = 0
		AND up.Authorised = 1
		INNER JOIN {databaseOwner}{objectQualifier}UserRoles AS ur 
		ON ur.UserID = u.UserID 
		AND (
					(ur.EffectiveDate IS NULL AND ur.ExpiryDate >= GETDATE()) /* DNN platform user roles still use native GETDATE() not GETUTCDATE() */
				OR (ur.EffectiveDate IS NULL AND ur.ExpiryDate IS NULL)
				OR (ur.EffectiveDate <= GETDATE() AND ur.ExpiryDate IS NULL)
				OR (ur.EffectiveDate <= GETDATE() AND ur.ExpiryDate >= GETDATE())
			)
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Functions_Split(@CanSubscribe,';') AS r 
		ON r.ID = ur.RoleId
		WHERE (s.Mode = @SubType)
		AND (S.ForumId = @ForumId AND S.TopicId = 0)
		AND (NOT EXISTS (SELECT * FROM @subs WHERE userid = s.UserId))
	)


/* get topic subscribers who are superusers  */
INSERT INTO @subs 
	(userid, email, topicsubscriber)
	(SELECT s.UserId, u.Email, 1 AS topicsubscriber  
		FROM {databaseOwner}{objectQualifier}activeforums_Subscriptions AS s 		
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums AS f 
		ON f.ForumId = s.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}Users AS u 
		ON  u.UserID = s.UserId 
		AND u.IsSuperUser = 1 
		AND u.IsDeleted = 0
		INNER JOIN {databaseOwner}{objectQualifier}UserPortals AS up 
		ON  up.UserId = u.UserID
		AND up.PortalId = @PortalId
		AND up.IsDeleted = 0
		AND up.Authorised = 1
		WHERE 	(s.Mode = @SubType)
		AND 	(s.TopicId = @TopicId)
		AND (NOT EXISTS (SELECT * FROM @subs WHERE userid = s.UserId))
	)

/* get forum subscribers who are superusers  */
INSERT INTO @subs 
	(userid, email, topicsubscriber)
	(SELECT s.UserId, u.Email, 0 AS topicsubscriber  
		FROM {databaseOwner}{objectQualifier}activeforums_Subscriptions AS s 		
		INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums AS f 
		ON f.ForumId = s.ForumId
		INNER JOIN {databaseOwner}{objectQualifier}Users AS u 
		ON  u.UserID = s.UserId 
		AND u.IsSuperUser = 1 
		AND u.IsDeleted = 0
		INNER JOIN {databaseOwner}{objectQualifier}UserPortals AS up 
		ON  up.UserId = u.UserID
		AND up.PortalId = @PortalId
		AND up.IsDeleted = 0
		AND up.Authorised = 1
		WHERE (s.Mode = @SubType)
		AND (S.ForumId = @ForumId AND S.TopicId = 0)
		AND (NOT EXISTS (SELECT * FROM @subs WHERE userid = s.UserId))
	)

/* get auto subscriptions based on roles */
DECLARE @AutoSubscribe bit
DECLARE @AutoSubscribeRoles nvarchar(255)
DECLARE @TopicsOnly bit
DECLARE @IsNewTopic bit
SET @AutoSubscribe = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings as S INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as F ON F.ForumSettingsKey = S.SettingsKey  WHERE S.SettingName = 'AUTOSUBSCRIBEENABLED' AND F.ForumId  = @ForumId),0)
SET @AutoSubscribeRoles = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings as S INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as F ON F.ForumSettingsKey = S.SettingsKey  WHERE S.SettingName = 'AUTOSUBSCRIBEROLES' AND F.ForumId  = @ForumId),'')
SET @TopicsOnly = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings as S INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums as F ON F.ForumSettingsKey = S.SettingsKey  WHERE S.SettingName = 'AUTOSUBSCRIBENEWTOPICSONLY' AND F.ForumId  = @ForumId),0)
SET @IsNewTopic = 0
IF (SELECT ReplyCount FROM {databaseOwner}{objectQualifier}activeforums_Topics WHERE TopicId = @TopicId) > 0
	SET @IsNewTopic = 1

If (@TopicsOnly = 1 AND @IsNewTopic = 0) OR (@TopicsOnly = 0)
	BEGIN
	IF @AutoSubscribe = 1 AND @AutoSubscribeRoles <> ''
	BEGIN
		
			INSERT INTO @subs 
			(userid, email, topicsubscriber)
			(SELECT u.UserID, u.Email, 0 AS topicsubscriber  /*auto subscribers are never topic-specific subscribers */
				FROM {databaseOwner}{objectQualifier}Users AS u 
				INNER JOIN {databaseOwner}{objectQualifier}UserPortals AS up 
				ON  up.UserId = u.UserID
				AND up.PortalId = @PortalId
				AND up.IsDeleted = 0
				AND up.Authorised = 1
				INNER JOIN {databaseOwner}{objectQualifier}UserRoles AS ur 
				ON ur.UserID = u.UserID 
				AND (
							(ur.EffectiveDate IS NULL AND ur.ExpiryDate >= GETDATE()) /* DNN platform user roles still use native GETDATE() not GETUTCDATE() */
						OR (ur.EffectiveDate IS NULL AND ur.ExpiryDate IS NULL)
						OR (ur.EffectiveDate <= GETDATE() AND ur.ExpiryDate IS NULL)
						OR (ur.EffectiveDate <= GETDATE() AND ur.ExpiryDate >= GETDATE())
					)
				INNER JOIN {databaseOwner}{objectQualifier}activeforums_Functions_Split(@AutoSubscribeRoles,';') AS r 
				ON r.ID = ur.RoleId
				WHERE (u.IsDeleted = 0)
				AND (NOT EXISTS (SELECT * FROM @subs WHERE userid = u.UserID))
			)

		END
	END
	
/* return the results */	
SELECT DISTINCT userid, email, topicsubscriber FROM @subs
GO



/* activeforums_Forums_List */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Forums_List]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Forums_List]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Forums_List]
@ModuleId int,
@ForumGroupId int,
@ParentForumId int,
@FillLastPost bit
AS
IF @ForumGroupId = -1 AND @ParentForumId = -1 AND @FillLastPost = 0
	--Return all Forums
SELECT     IsNull(F.ForumId,-1) as ForumId, G.ModuleId, G.ForumGroupId, IsNull(F.ParentForumId,'-1') as ParentForumId, IsNull(F.ForumName,'') as ForumName, F.ForumDesc, F.SortOrder, F.Active, F.Hidden, F.TotalTopics, 
                      F.TotalReplies, F.LastTopicId, F.LastReplyId, F.ForumSettingsKey, G.GroupName, ISNULL(P.ForumName, '') AS ParentForumName,
		TopicsTemplateId = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICSTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0),
		TopicTemplateId =  IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0)
FROM         {databaseOwner}{objectQualifier}activeforums_Forums AS F RIGHT OUTER JOIN
                      {databaseOwner}{objectQualifier}activeforums_Groups AS G ON F.ForumGroupId = G.ForumGroupId LEFT OUTER JOIN
                      {databaseOwner}{objectQualifier}activeforums_Forums AS P ON F.ParentForumId = P.ForumId
WHERE     (G.ModuleId = @ModuleId)
ORDER BY G.SortOrder, F.SortOrder

ELSE
	IF @ForumGroupId > 0 AND @ParentForumId = -1 AND @FillLastPost = 0
		--Return Forums in Group
		SELECT     F.ForumId, F.ModuleId, F.ForumGroupId, F.ParentForumId, F.ForumName, F.ForumDesc, F.SortOrder, F.Active, F.Hidden, F.TotalTopics, 
						  F.TotalReplies, F.LastTopicId, F.LastReplyId, F.ForumSettingsKey, G.GroupName, ISNULL(P.ForumName, '') AS ParentForumName,
		TopicsTemplateId = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICSTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0),
		TopicTemplateId =  IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0)
		FROM         {databaseOwner}{objectQualifier}activeforums_Forums AS F INNER JOIN
						  {databaseOwner}{objectQualifier}activeforums_Groups AS G ON F.ForumGroupId = G.ForumGroupId LEFT OUTER JOIN
						  {databaseOwner}{objectQualifier}activeforums_Forums AS P ON F.ParentForumId = P.ForumId
		WHERE F.ModuleId = @ModuleId AND F.ForumGroupId = @ForumGroupId
		ORDER By F.SortOrder
	ELSE
		If @ForumGroupId > 0 AND @ParentForumId > 0 AND @FillLastPost = 0
			--Return Sub Forums
			
			SELECT     F.ForumId, F.ModuleId, F.ForumGroupId, F.ParentForumId, F.ForumName, F.ForumDesc, F.SortOrder, F.Active, F.Hidden, F.TotalTopics, 
							  F.TotalReplies, F.LastTopicId, F.LastReplyId, F.ForumSettingsKey, G.GroupName, ISNULL(P.ForumName, '') AS ParentForumName,
		TopicsTemplateId = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICSTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0),
		TopicTemplateId =  IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0)
			FROM         {databaseOwner}{objectQualifier}activeforums_Forums AS F INNER JOIN
							  {databaseOwner}{objectQualifier}activeforums_Groups AS G ON F.ForumGroupId = G.ForumGroupId LEFT OUTER JOIN
							  {databaseOwner}{objectQualifier}activeforums_Forums AS P ON F.ParentForumId = P.ForumId
			WHERE F.ModuleId = @ModuleId AND F.ParentForumId = @ParentForumId
			ORDER By G.SortOrder, F.SortOrder
		ELSE
			---Returns Forums In Group with Last Post Info
SELECT     F.ForumId, F.ModuleId, F.ForumGroupId, F.ParentForumId, F.ForumName, F.ForumDesc, F.SortOrder, F.Active, F.Hidden, F.TotalTopics, 
                      F.TotalReplies, F.LastTopicId, F.LastReplyId, F.ForumSettingsKey, G.GroupName, ISNULL(P.ForumName, '') AS ParentForumName, ISNULL(U.FirstName, '') 
                      AS LastPostFirstName, ISNULL(U.LastName, '') AS LastPostLastName, ISNULL(C.AuthorId, - 1) AS LastPostUserId, ISNULL(C.AuthorName, '') AS LastPostUserName, 
                      C.Subject AS LastPostSubject, C.DateCreated AS LastPostDateTime,
		TopicsTemplateId = IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICSTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0),
		TopicTemplateId =  IsNull((SELECT SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingName = 'TOPICTEMPLATEID' and SettingsKey = F.ForumSettingsKey),0)
FROM         {databaseOwner}{objectQualifier}activeforums_Content AS C INNER JOIN
                      {databaseOwner}{objectQualifier}activeforums_Topics AS T ON C.ContentId = T.ContentId INNER JOIN
                      {databaseOwner}{objectQualifier}activeforums_UserProfiles AS UD INNER JOIN
                      {databaseOwner}{objectQualifier}Users AS U ON UD.UserID = U.UserID ON C.AuthorId = UD.UserID RIGHT OUTER JOIN
                      {databaseOwner}{objectQualifier}activeforums_Forums AS F INNER JOIN
                      {databaseOwner}{objectQualifier}activeforums_Groups AS G ON F.ForumGroupId = G.ForumGroupId ON T.TopicId = F.LastTopicId LEFT OUTER JOIN
                      {databaseOwner}{objectQualifier}activeforums_Forums AS P ON F.ParentForumId = P.ForumId
WHERE     (F.ModuleId = @ModuleId) AND (F.ForumGroupId = @ForumGroupId)
ORDER BY G.SortOrder, F.SortOrder
GO

IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_MC_Forums]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_MC_Forums]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_MC_Forums]
AS
DECLARE @SettingName nvarchar(100)
DECLARE @SettingValue nvarchar(100)
SET @SettingName = 'MCENABLED'
SET @SettingValue = 'true'
DECLARE @MailForums table (forumid int)
INSERT INTO @MailForums(forumid)
Select f.ForumId From {databaseOwner}{objectQualifier}activeforums_Forums as F 
	INNER JOIN {databaseOwner}{objectQualifier}Modules as M ON F.ModuleId = M.ModuleId
	INNER JOIN {databaseOwner}{objectQualifier}activeforums_Settings as S ON S.SettingsKey = F.ForumSettingsKey
WHERE M.IsDeleted = 0 AND F.Active = 1 AND S.SettingValue = @SettingValue

SELECT *, TM.TabId from {databaseOwner}{objectQualifier}activeforums_Forums as F INNER JOIN @MailForums as M ON M.forumid = F.ForumId
	INNER JOIN {databaseOwner}{objectQualifier}TabModules as TM ON F.ModuleId = TM.ModuleId
GO



/* activeforums_Forum_ConfigCleanUp */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_Forum_ConfigCleanUp') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}{objectQualifier}activeforums_Forum_ConfigCleanUp
GO
CREATE PROCEDURE {databaseOwner}{objectQualifier}activeforums_Forum_ConfigCleanUp
@ModuleId int,
@ForumSettingsKey nvarchar(25)
AS
DELETE FROM {databaseOwner}{objectQualifier}activeforums_Settings WHERE SettingsKey = @ForumSettingsKey AND ModuleId = @ModuleId
GO


        
/* begin: activeforums_Search_GetSearchItemsFromBegDate */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Search_GetSearchItemsFromBegDate]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Search_GetSearchItemsFromBegDate]
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}activeforums_Search_GetSearchItemsFromBegDate(@ModuleId int, @beginDateUtc datetime)
AS
SELECT F.ForumGroupId, FT.ForumId, X.TopicId, X.ReplyId, X.ContentId, X.DateCreated, X.DateUpdated, X.Summary, X.Subject, X.AuthorId, X.Body , X.IsDeleted, X.IsApproved,
RTRIM(IsNull([dbo].activeforums_Topics_GetTags(X.TopicId),'')) AS Tags, X.TopicURL, F.PrefixURL AS ForumUrlPrefix, G.PrefixURL AS ForumGroupUrlPrefix
FROM 
(
SELECT 
T.TopicId, -1 AS ReplyId, C.ContentId, C.DateCreated, C.DateUpdated, C.Summary, C.Subject, C.AuthorId, C.Body, T.IsDeleted, T.IsApproved, T.URL AS TopicUrl
FROM {databaseOwner}{objectQualifier}activeforums_Topics T
INNER JOIN {databaseOwner}{objectQualifier}activeforums_Content C ON C.ContentId = T.ContentId 
WHERE C.DateUpdated BETWEEN @beginDateUtc AND GETUTCDATE() 
UNION
SELECT
R.TopicId, R.ReplyId, C.ContentId, C.DateCreated, C.DateUpdated, C.Summary, C.Subject, C.AuthorId, C.Body, R.IsDeleted, R.IsApproved, T.URL AS TopicUrl
FROM {databaseOwner}{objectQualifier}activeforums_Replies R
INNER JOIN {databaseOwner}{objectQualifier}activeforums_Content C ON C.ContentId = R.ContentId 
LEFT OUTER JOIN {databaseOwner}{objectQualifier}activeforums_Topics T ON T.TopicId = R.TopicId
WHERE C.DateUpdated BETWEEN @beginDateUtc AND GETUTCDATE() 
) X
INNER JOIN {databaseOwner}{objectQualifier}activeforums_Topics T ON T.TopicId = X.TopicId
INNER JOIN {databaseOwner}{objectQualifier}activeforums_ForumTopics FT ON FT.TopicId = T.TopicId 
INNER JOIN {databaseOwner}{objectQualifier}activeforums_Forums F ON F.ForumId = FT.ForumId 
INNER JOIN {databaseOwner}{objectQualifier}activeforums_Groups G ON G.ForumGroupId  = F.ForumGroupId 
INNER JOIN {databaseOwner}{objectQualifier}activeforums_settings S ON S.SettingsKey = F.ForumSettingsKey AND S.SettingName = 'INDEXCONTENT'
WHERE F.Active = 1 AND S.SettingValue = 'true' AND F.ModuleId = @ModuleId 
GO

		-- set last topic / reply on old Forum
		SET @LastReplyId = (SELECT MAX(r.ReplyId) FROM {databaseOwner}{objectQualifier}activeforums_Replies as r inner join {databaseOwner}{objectQualifier}activeforums_topics as t on r.topicid = t.topicid inner join {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on t.topicid = ft.topicid  WHERE r.IsApproved = 1 AND r.IsDeleted = 0 AND ft.forumid = @OldForumId )
		SET @LastTopicId = (SELECT MAX(t.TopicId) FROM {databaseOwner}{objectQualifier}activeforums_Topics as t inner join {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on t.topicid = ft.topicid  WHERE t.IsApproved = 1 AND t.IsDeleted = 0 AND ft.forumid = @OldForumId )
		UPDATE {databaseOwner}{objectQualifier}activeforums_Forums SET LastReplyId = IsNull(@LastReplyId,0), LastTopicId = ISNULL(@LastTopicId,0) WHERE ForumId = @OldForumId

/* activeforums_Settings_Delete */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings_Delete]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_Delete]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_Delete]
(
	@ModuleId int,
	@SettingsKey nvarchar(25),
	@SettingName nvarchar(50)
)
AS
DELETE FROM {databaseOwner}[{objectQualifier}activeforums_Settings] WHERE ModuleId = @ModuleId AND SettingName = @SettingName AND SettingsKey = @SettingsKey
GO

/* activeforums_Settings_Save */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings_Save]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_Save]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_Save]
(
	@ModuleId int,
	@SettingsKey nvarchar(25),
	@SettingName nvarchar(50),
	@SettingValue nvarchar(2000)
)
AS
If EXISTS(SELECT SettingName FROM {databaseOwner}[{objectQualifier}activeforums_Settings] WHERE ModuleId = @ModuleId AND SettingName = @SettingName AND SettingsKey = @SettingsKey)
	BEGIN
		UPDATE {databaseOwner}[{objectQualifier}activeforums_Settings]
		SET SettingValue = @SettingValue
		 WHERE ModuleId = @ModuleId AND SettingName = @SettingName AND SettingsKey = @SettingsKey
	END
ELSE
	INSERT INTO {databaseOwner}[{objectQualifier}activeforums_Settings]
		(ModuleId, SettingsKey, SettingName, SettingValue)
		VALUES
		(@ModuleId, @SettingsKey, @SettingName, @SettingValue)

GO

/* activeforums_Settings_Delete */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings_Get]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_Get]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_Get]
(
	@ModuleId int,
	@SettingsKey nvarchar(25),
	@SettingName nvarchar(50)
)
AS
SELECT SettingValue FROM {databaseOwner}[{objectQualifier}activeforums_Settings] WHERE ModuleId = @ModuleId AND SettingName = @SettingName AND SettingsKey = @SettingsKey
GO

/* activeforums_Settings_List */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings_List]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_List]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_List]
(
	@ModuleId int,
	@SettingsKey nvarchar(25)
)
AS
SELECT SettingName, SettingValue FROM {databaseOwner}{objectQualifier}activeforums_Settings 
WHERE ModuleId = @ModuleId AND SettingsKey = @SettingsKey 
GO


/* activeforums_Settings_ListAll */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Settings_ListAll]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_ListAll]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Settings_ListAll] 
	@ModuleId INT
	
AS 
SELECT  SettingName ,
        SettingValue,
        SettingsKey
FROM    {databaseOwner}{objectQualifier}activeforums_Settings
WHERE   ModuleId = @ModuleId
GO



/*drop activeforums_ForumsList*/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_ForumsList]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_ForumsList]
GO

/* drop activeforums_SettingsTable */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_SettingsTable]') AND type in (N'FN', N'IF', N'TF', N'FS', N'FT'))
DROP FUNCTION {databaseOwner}[{objectQualifier}activeforums_SettingsTable]
GO


/* issue 1582 - end - settings table overhaul */

/* --------------------- */


/* Issue 1594 - Begin - update activeforums_Topics_Move procedure to remove previously-removed LastPostId from activeforums_Forums */
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Topics_Move]') AND type in (N'P', N'PC'))
DROP PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Topics_Move]
GO
CREATE PROCEDURE {databaseOwner}[{objectQualifier}activeforums_Topics_Move]
@PortalId int,
@ModuleId int,
@ForumId int,
@TopicId int
AS
DECLARE @OldForumId int
SELECT @OldForumId = ForumId FROM {databaseOwner}{objectQualifier}activeforums_ForumTopics WHERE TopicId = @TopicId
If @OldForumId <> @ForumId 
	BEGIN
		UPDATE {databaseOwner}{objectQualifier}activeforums_ForumTopics SET ForumId = @ForumId WHERE TopicId = @TopicId AND ForumId = @OldForumId
        
		DECLARE @LastReplyId int
		DECLARE @LastTopicId int

		-- set last topic / reply on new Forum
		SET @LastReplyId = (SELECT MAX(r.ReplyId) FROM {databaseOwner}{objectQualifier}activeforums_Replies as r inner join {databaseOwner}{objectQualifier}activeforums_topics as t on r.topicid = t.topicid inner join {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on t.topicid = ft.topicid  WHERE r.IsApproved = 1 AND r.IsDeleted = 0 AND ft.forumid = @ForumId)
		SET @LastTopicId = (SELECT MAX(t.TopicId) FROM {databaseOwner}{objectQualifier}activeforums_Topics as t inner join {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on t.topicid = ft.topicid  WHERE t.IsApproved = 1 AND t.IsDeleted = 0 AND ft.forumid = @ForumId)
		UPDATE {databaseOwner}{objectQualifier}activeforums_Forums SET LastReplyId = IsNull(@LastReplyId,0), LastTopicId = ISNULL(@LastTopicId,0) WHERE ForumId = @ForumId

		-- set last topic / reply on old Forum
		SET @LastReplyId = (SELECT MAX(r.ReplyId) FROM {databaseOwner}{objectQualifier}activeforums_Replies as r inner join {databaseOwner}{objectQualifier}activeforums_topics as t on r.topicid = t.topicid inner join {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on t.topicid = ft.topicid  WHERE r.IsApproved = 1 AND r.IsDeleted = 0 AND ft.forumid = @OldForumId )
		SET @LastTopicId = (SELECT MAX(t.TopicId) FROM {databaseOwner}{objectQualifier}activeforums_Topics as t inner join {databaseOwner}{objectQualifier}activeforums_ForumTopics as ft on t.topicid = ft.topicid  WHERE t.IsApproved = 1 AND t.IsDeleted = 0 AND ft.forumid = @OldForumId )
		UPDATE {databaseOwner}{objectQualifier}activeforums_Forums SET LastReplyId = IsNull(@LastReplyId,0), LastTopicId = ISNULL(@LastTopicId,0) WHERE ForumId = @OldForumId

        -- clean up tracking records for the old forum pointing to this topic
		DELETE FROM {databaseOwner}{objectQualifier}activeforums_Forums_Tracking WHERE ForumId = @OldForumId AND MaxTopicRead = @TopicId
		UPDATE {databaseOwner}{objectQualifier}activeforums_Topics_Tracking SET ForumId = @ForumId WHERE TopicId = @TopicId AND ForumId = @OldForumId

        -- move any subscriptions for this topic from the old forum to the new forum
		UPDATE {databaseOwner}{objectQualifier}activeforums_Subscriptions SET ForumId = @ForumId WHERE TopicId = @TopicId AND ForumId = @OldForumId
	END
GO
/* Issue 1594 - End - update activeforums_Topics_Move procedure to remove previously-removed LastPostId from activeforums_Forums */

/* --------------------- */
