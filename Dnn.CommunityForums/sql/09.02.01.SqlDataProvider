SET NOCOUNT ON 
GO

/* --------------------- */

/* issue 1492 - begin - Rename CanBan -> CanManageUsers in activeforums_Permissions */

IF EXISTS(SELECT * FROM sys.columns WHERE [name] = N'CanBan' AND [object_id] = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Permissions]'))
exec sp_rename '{databaseOwner}{objectQualifier}activeforums_Permissions.CanBan', 'CanManageUsers', 'COLUMN'
GO

IF EXISTS(SELECT * FROM sys.columns WHERE [name] = N'CanBlock' AND [object_id] = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Permissions]'))
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_Permissions] DROP COLUMN CanBlock
GO

/* issue 1492 - end - Rename CanBan -> CanManageUsers in activeforums_Permissions */

/* --------------------- */

/* issue 1619 - begin - fix missing cascade delete for activeforums_Badges from Modules */

/* activeforums_Badges - cascade delete from Modules */
IF EXISTS (SELECT * FROM sys.foreign_keys WHERE object_id = OBJECT_ID(N'{databaseOwner}[FK_{objectQualifier}activeforums_Badges_Modules]') AND parent_object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}activeforums_Badges]'))
ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_Badges] DROP CONSTRAINT 
[FK_{objectQualifier}activeforums_Badges_Modules]
GO

DELETE FROM b
FROM {databaseOwner}[{objectQualifier}activeforums_Badges] b
LEFT OUTER JOIN {databaseOwner}[{objectQualifier}Modules] m
ON m.ModuleID = b.ModuleId
WHERE m.ModuleID IS NULL
GO

ALTER TABLE {databaseOwner}[{objectQualifier}activeforums_Badges] ADD CONSTRAINT
	[FK_{objectQualifier}activeforums_Badges_Modules] FOREIGN KEY (ModuleId) 
	REFERENCES {databaseOwner}[{objectQualifier}Modules] (ModuleID) 
	ON DELETE CASCADE 
GO
/* issue 1619 - end - fix missing cascade delete for activeforums_Badges from Modules */

/* --------------------- */

/* issue 1620 - begin - keep badges within module instance */

/* drop/create index IX_activeforums_UserBadges_Alt3 */
IF EXISTS (SELECT * FROM sys.indexes WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}activeforums_UserBadges') AND name = N'IX_{objectQualifier}activeforums_UserBadges_Alt3')
DROP INDEX [IX_{objectQualifier}activeforums_UserBadges_Alt1] ON {databaseOwner}{objectQualifier}activeforums_UserBadges 
GO
CREATE NONCLUSTERED INDEX IX_{objectQualifier}activeforums_UserBadges_Alt3
    ON {databaseOwner}[{objectQualifier}activeforums_UserBadges] 
    (
    PortalId ASC,
    ModuleId ASC,
    UserId ASC,
    BadgeId ASC,
    DateAssigned ASC
    ) 

GO

/* issue 1620 - end - keep badges within module instance */

/* --------------------- */